<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.4">
  <POU Name="F_STRTOANY" Id="{bcf1633a-b298-4d6c-9ca4-8a73f63bdec2}" SpecialFunc="None">
    <Declaration><![CDATA[// Converts string to any, returns TRUE if successful
FUNCTION F_STRTOANY : BOOL
VAR_INPUT
	 Value : ANY;
	 sValue : T_Long_String;
END_VAR
VAR	
	pBool			: POINTER TO BOOL;
    pByte			: POINTER TO BYTE;
    pWord			: POINTER TO WORD;
    pDWord			: POINTER TO DWORD;
    pLWord			: POINTER TO LWORD;
    pSint			: POINTER TO SINT;
    pInt			: POINTER TO INT;
    pDint			: POINTER TO DINT;
    pLint			: POINTER TO LINT;
    pUSint			: POINTER TO USINT;
    pUInt			: POINTER TO UINT;
    pUDint			: POINTER TO UDINT;
    pULint			: POINTER TO ULINT;
    pReal			: POINTER TO REAL;
    pLReal			: POINTER TO LREAL;
    pString			: POINTER TO T_Long_String;
    pWString		: POINTER TO WSTRING;
    pTime			: POINTER TO TIME;
    pDate			: POINTER TO DATE;
    pDate_And_Time	: POINTER TO DATE_AND_TIME;
    pTime_Of_Day	: POINTER TO TIME_OF_DAY;
    pPointer		: POINTER TO PVOID;
	pLTime 			: POINTER TO LTIME;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[F_STRTOANY := TRUE;
IF Value.pValue = 0 THEN F_STRTOANY := FALSE; RETURN; END_IF;

CASE Value.TypeClass OF 
	__SYSTEM.TYPE_CLASS.TYPE_BOOL:
		IF NOT F_ISBOOL(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pBool := Value.pValue;
		pBool^ := TO_BOOL(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_BYTE:
		IF NOT F_ISUNSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pByte := Value.pValue;
		pByte^ := TO_BYTE(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_WORD:
		pWord := Value.pValue;
		pWord^ := TO_WORD(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_DWORD:
		pDWord := Value.pValue;
		pDWord^ := TO_DWORD(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_LWORD:
		pLWord := Value.pValue;
		pLWord^ := TO_LWORD(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_SINT:
		IF NOT F_ISSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pSint := Value.pValue;
		pSint^ := TO_SINT(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_INT:
		IF NOT F_ISSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pInt := Value.pValue;
		pInt^ := TO_INT(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_DINT:
		IF NOT F_ISSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pDint := Value.pValue;
		pDint^ := TO_DINT(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_LINT:
		IF NOT F_ISSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pLint := Value.pValue;
		pLint^ := TO_LINT(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_USINT:
		IF NOT F_ISUNSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pUSint := Value.pValue;
		pUSint^ := TO_USINT(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_UINT:
		IF NOT F_ISUNSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pUInt := Value.pValue;
		pUInt^ := TO_UINT(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_UDINT:
		IF NOT F_ISUNSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pUDint := Value.pValue;
		pUDint^ := TO_UDINT(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_ULINT:
		IF NOT F_ISUNSIGNEDINT(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pULint := Value.pValue;
		pULint^ := TO_ULINT(sValue);
		
   __SYSTEM.TYPE_CLASS.TYPE_REAL:
   		IF NOT F_ISNUM(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pReal := Value.pValue;
		pReal^ := TO_REAL(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_LREAL:
		IF NOT F_ISNUM(sValue) THEN F_STRTOANY := FALSE; RETURN; END_IF
		pLReal := Value.pValue;
		pLReal^ := TO_LREAL(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_STRING:
		F_STRLCPY(Value.pValue, ADR(sValue),Value.diSize); 
		
   __SYSTEM.TYPE_CLASS.TYPE_WSTRING:
   		Tc2_Utilities.STRING_TO_WSTRING2(Value.pValue, ADR(sValue), TO_UDINT(Value.diSize));
		
    __SYSTEM.TYPE_CLASS.TYPE_TIME:
		pTime := Value.pValue;
		pTime^ := TO_TIME(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_DATE:
		pDate := Value.pValue;
		pDate^ := TO_DATE(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_DATEANDTIME:
		pDate_And_Time := Value.pValue;
		pDate_And_Time^ := TO_DT(sValue);
		
    __SYSTEM.TYPE_CLASS.TYPE_TIMEOFDAY:
		pTime_Of_Day := Value.pValue;
		pTime_Of_Day^ := TO_TOD(sValue);
	
	__SYSTEM.TYPE_CLASS.TYPE_POINTER:
		pPointer := Value.pValue;
		pPointer^ := Tc2_Utilities.STRING_TO_PVOID(sValue);
		
	__SYSTEM.TYPE_CLASS.TYPE_LTIME:
		pLTime := Value.pValue;
		pLTime^ := TO_LTIME(sValue);
ELSE
	F_STRTOANY := FALSE;
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="F_STRTOANY">
      <LineId Id="289" Count="0" />
      <LineId Id="160" Count="3" />
      <LineId Id="323" Count="0" />
      <LineId Id="164" Count="3" />
      <LineId Id="324" Count="0" />
      <LineId Id="168" Count="15" />
      <LineId Id="356" Count="0" />
      <LineId Id="184" Count="3" />
      <LineId Id="357" Count="0" />
      <LineId Id="188" Count="3" />
      <LineId Id="358" Count="0" />
      <LineId Id="192" Count="3" />
      <LineId Id="359" Count="0" />
      <LineId Id="196" Count="3" />
      <LineId Id="360" Count="0" />
      <LineId Id="200" Count="3" />
      <LineId Id="361" Count="0" />
      <LineId Id="204" Count="3" />
      <LineId Id="362" Count="0" />
      <LineId Id="208" Count="3" />
      <LineId Id="363" Count="0" />
      <LineId Id="212" Count="3" />
      <LineId Id="366" Count="0" />
      <LineId Id="216" Count="3" />
      <LineId Id="367" Count="0" />
      <LineId Id="220" Count="3" />
      <LineId Id="286" Count="0" />
      <LineId Id="226" Count="1" />
      <LineId Id="287" Count="0" />
      <LineId Id="230" Count="17" />
      <LineId Id="288" Count="0" />
      <LineId Id="248" Count="6" />
      <LineId Id="7" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>